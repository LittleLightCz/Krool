buildscript {
    ext.kotlin_version = '1.3.0-rc-190'

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

def kroolGroup = "com.svetylkovo"
def kroolArtifact = "krool"
def kroolVersion = "0.1.0"

def kroolDescription = "Kotlin resource pool based on non-blocking coroutines."

def kroolLicence = "GPL-3.0"
def kroolVcsUrl = "https://github.com/LittleLightCz/Krool.git"

group = kroolGroup
version = kroolVersion

allprojects {
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.30.2-eap13"
    testImplementation "junit:junit:4.12"
    testImplementation "org.assertj:assertj-core:3.10.0"
    testImplementation "io.mockk:mockk:1.8.5"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId kroolGroup
            artifactId kroolArtifact
            version kroolVersion
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

Properties props = new Properties()
new File("$rootDir/bintray.properties").withInputStream {
    props.load(it)
}

bintray {
    user = props.getProperty('bintray.user')
    key = props.getProperty('bintray.apikey')
    publications = ['MyPublication']
    override = true

    pkg {
        repo = 'Krool'
        name = kroolArtifact
        userOrg = user
        licenses = [kroolLicence]
        vcsUrl = kroolVcsUrl
        labels = ['kotlin', 'pool', 'resource']
        publicDownloadNumbers = true
        version {
            name = kroolVersion
            desc = kroolDescription
            vcsTag = kroolVersion
        }
    }
}